program = { SOI ~ declaration+ ~ EOI }
declaration = { include | function | polyinst | name }
polyinst = { name ~ (bound)* }
bound = {"[" ~ float ~ "," ~ float ~ "]"}

keywords = { "let" | "lcb" | "lb" | "case"| "in" | "of" | "=" }

function = { "function" ~ name ~ args ~ (":" ~ type)? ~ body }
name = @{ 
  !keywords ~
  ASCII_ALPHA ~ ("_" | "-" | "'" | ASCII_ALPHANUMERIC)* 
  }

args = { arg* }
arg = { "(" ~ name ~ ":" ~ type ~ ")" }
type = { 
  monad | bang | forall | fun |
  unit | bang | num | tensor | cartesian | sum |
  "(" ~ type ~ ")"
}
unit = { "()" }
num = { "num" | "num[" ~ iop ~ "]" }
tensor = { "(" ~ type ~ "," ~ type ~ ")" }
cartesian = { "<" ~ type ~ "," ~ type ~ ">" }
sum = { atom_ty ~ "+" ~ type }
fun = { atom_ty ~ "-o" ~ type }
monad = { "M[" ~ (eps | float) ~ "]" ~ type }
bang = { ("![" ~ float ~ "]"~ type) }
forall = { "forall" ~ name ~ "." ~ type }

atom_ty = { unit | num | "(" ~ type ~ ")" }

iop = { name ~ "(" ~ float ~ "," ~ float ~ ")" | lifted_op ~ iop ~ iop }
lifted_op = { "+" | "*" | "-" }

body = { "{" ~ expr ~ "}" }
expr = {
  ret | rnd |
  letassign | lcb | lb | lp |
  float | abs | app | op | proj | 
  tens | cart | in | 
  case | factor | scale |
  var | atom
}
var = { name }
letassign = { 
  ("let " ~ name ~ "=" ~ expr ~ " in " ~ expr) 
  | (name ~ "=" ~ expr ~ ";" ~ expr) // old syntax
}
lcb = { 
  ("lcb " ~ name ~ "=" ~ expr ~ "in" ~ expr) 
  | ("let" ~ "[" ~ name ~ "]" ~ "=" ~ expr ~ ";" ~ expr) // old syntax
}
lb = { 
  ("lb " ~ name ~ "=" ~ expr ~ "in" ~ expr) 
  | ("let" ~ name ~ "=" ~ expr ~ ";" ~ expr) // old syntax
}
lp = { 
  ("lp " ~ name ~ "=" ~ expr ~ " in " ~ expr) 
  | ("let" ~ "(" ~ name ~ "," ~ name ~ ")" ~ "=" ~ expr ~ ";" ~ expr) // old syntax
}
op = { "add " | "sub " | "mul " }
abs = { "fun" ~ args ~ body }
app = { 
  atom ~ expr
  // (atom ~ WHITESPACE ~ expr) | (atom ~ WHITESPACE? ~ &"(" ~ WHITESPACE? ~ expr) 
}
atom = { op | factor | var | float | "(" ~ expr ~ ")" }
proj = { "fst" ~ expr | "snd" ~ expr }
tens = { "(" ~ expr ~ "," ~ expr ~ ")" }
cart = { 
  ("|" ~ expr ~ "," ~ expr ~ "|") | 
  ("<" ~ expr ~ "," ~ expr ~ ">") // old syntax
}
in = { "inl" ~ expr | "inr" ~ expr }
case = { 
  "case" ~ expr ~ 
    "{" ~ 
      "inl" ~ expr ~ "=>" ~ expr 
      ~ "|" ~ 
      "inr" ~ expr ~ "=>" ~ expr ~ 
    "}"
  }
ret = { "ret" ~ expr }
rnd = { "rnd" ~ modes ~ expr }
factor = { "factor" ~ expr }
scale = { "[" ~ expr ~ "{" ~ float ~ "}" ~ "]" }
modes = { "16" | "32" | "64" }


include = { "#include" ~ "\"" ~ filename ~ "\"" ~ (";")? }
filename = { path ~ ".fz" }
path = @{ (".." | "."  | name ) ~ ("/" ~ name)* }

// Numbers, taken from the pest docs
integer = { ("+" | "-")? ~ int }
eps = @{ "eps" ~ modes ~ "_" ~ ("up" | "down") }
float   = { ("+" | "-")? ~ int ~ ("." ~ digits ~ exp? | exp)? }
int     = { "0" | (ASCII_NONZERO_DIGIT ~ digits?) }
digits  = { (ASCII_DIGIT | ("_" ~ ASCII_DIGIT))+ }
exp     = { ("E" | "e") ~ ("+" | "-")? ~ int }

space = _{ " " | NEWLINE | WHITE_SPACE }
WHITESPACE = _{ " " | NEWLINE | WHITE_SPACE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE | "/*" ~ (ASCII_ALPHANUMERIC | WHITESPACE)* ~ "*/"}
